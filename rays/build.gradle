//
//
//
//
// TO COMPILE TEST RESULTS FROM ALL SUBPROJECTS
// gradle allTests
//
//
// TO GENERATE BASE DB SCHEMAS (HSQLDB,MySQL) FOR :frontend
// gradle :frontend:generateSchema
//
//
// TO BUILD DOCKER IMAGES:
//
// ./gradlew docker dockerTag
//     OR
// ./gradlew :worker:docker :worker:dockerTag :frontend:docker :frontend:dockerTag
//
//
// TO GENERATE docker-compose.yml FILE FROM ITS TEMPLATE
// gradle generateDockerCompose
//
//
// TO START docker-compose.yml CONFIGURATION
// docker-compose up --d --scale worker=[WORKER-COUNT]
//

buildscript {
	ext { springBootVersion = '2.1.6.RELEASE' }
	repositories { mavenCentral() }
	dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") }
}

plugins {
	id 'com.palantir.docker' version '0.20.1' apply false
	id 'com.palantir.docker-compose' version '0.20.1'
	id 'com.google.osdetector' version '1.6.2'
}

group = 'org.snowjak'
version = '0.4.3'
wrapper { gradleVersion = 5.5 }


project.ext.dockerComposeTemplateName = "docker-compose.yml.template"

if ( file( dockerComposeTemplateName + "." + osdetector.arch ).exists() )
	dockerComposeTemplateName = dockerComposeTemplateName + "." + osdetector.arch


allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'java'
	
	repositories {
		mavenCentral()
		jcenter()
		maven { url "http://maven.vaadin.com/vaadin-addons" }
	}
	
	sourceCompatibility = 11
	
	tasks.withType(JavaCompile) { options.compilerArgs << '-parameters' }
}

//
// All projects which will execute as Spring Boot applications
project.ext.bootableProjects = [
	"frontend",
	"spectrum-generator",
	"worker"
]

//
// All projects which have :core as a dependency
project.ext.coreableProjects = [
	"frontend",
	"spectrum-generator",
	"support",
	"worker"
]

//
// All projects which have :support as a dependency
project.ext.supportableProjects = ["frontend", "worker"]

//
// All projects which will produce a Docker image
project.ext.dockerableProjects = ["frontend", "worker"]


subprojects { p ->
	
	p.group = rootProject.group
	p.version = rootProject.version
	
	apply plugin: 'io.spring.dependency-management'
	
	
	ext { vaadinVersion = '8.+' }
	
	dependencyManagement {
		imports {
			mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
			mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
		}
	}
	
	
	
	//
	// If this project depends on :core ...
	//
	if ( coreableProjects.contains( p.name ) ) {
		
		dependencies { compile project(":core") }
		
		clean.dependsOn( project(":core").clean )
		test.dependsOn( project(":core").test )
		
		clean.delete << file('data/')
		
		task copyDataFromCore(type: Copy) {
			from project(":core").file( "data/")
			into p.file("data/")
			duplicatesStrategy = 'include'
		}
		processResources.dependsOn( copyDataFromCore )
	}
	
	
	//
	// If this project depends on :support ...
	//
	if ( supportableProjects.contains( p.name ) ) {
		
		dependencies { compile project(":support") }
		
		clean.dependsOn( project(":support").clean )
		test.dependsOn( project(":support").test )
	}
	
	
	//
	// If this project will execute as a Spring Boot application ...
	//
	if ( bootableProjects.contains ( p.name ) ) {
		apply plugin: 'org.springframework.boot'
	}
	
	
	//
	// If this project will produce a Docker image ...
	//
	if ( dockerableProjects.contains( p.name ) ) {
		apply plugin: 'com.palantir.docker'
		
		p.ext.dockerFileName = "Dockerfile"
		
		if ( file( dockerFileName + "." + osdetector.arch ).exists() )
			dockerFileName = dockerFileName + "." + osdetector.arch
		
		
		task copyDataToDockerBuildDir(type: Copy) {
			from 'data/'
			into "$buildDir/docker/data"
			duplicatesStrategy = 'include'
			
			dependsOn 'copyDataFromCore'
			dependsOn 'dockerPrepare'
		}
		
		docker {
			name = "${p.group}/${p.name}:${p.version}"
			dockerfile p.file( dockerFileName )
			tags "latest"
			files p.jar.archivePath
			buildArgs([ JAR_FILE: p.jar.archiveName, DATA_DIR: 'data' ])
			pull true
		}
		
		p.tasks.docker.dependsOn { 'copyDataToDockerBuildDir' }
		
		p.tasks.docker.dependsOn {'bootJar' }
		p.tasks.dockerPush.dependsOn { 'dockerTag' }
	}
	
	
	test.ignoreFailures = true
}

task allTests(type: TestReport) {
	group = "Verification"
	description = "Gathers the output from all subproject 'test' tasks"
	destinationDir = file("$buildDir/reports/allTests/")
	
	dependsOn subprojects.collect( { it.tasks.withType(Test) } ).flatten()
	reportOn subprojects.collect( { it.tasks.withType(Test) } ).flatten()
}

clean.delete << file('build/docker-compose.yml.template.expanded')

task expandDockerComposeTemplate(type: Copy) {
	from dockerComposeTemplateName
	expand( project.properties )
	into 'build'
	rename { "docker-compose.yml.template.expanded" }
	duplicatesStrategy 'INCLUDE'
	
	dependsOn ":worker:docker"
	dependsOn ":frontend:docker"
}

dockerCompose {
	template project.file( "build/docker-compose.yml.template.expanded" )
}

generateDockerCompose {
	dependsOn "expandDockerComposeTemplate"
}
