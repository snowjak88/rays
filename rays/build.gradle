buildscript {
	ext {
		springBootVersion = '2.0.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

allprojects {
	apply plugin: 'eclipse'
	
	wrapper {
		gradleVersion = 4.6
	}
}

subprojects { p ->
		
	apply plugin: 'java'
	
	if( p.name == "core" ) {
		test { reports.html.enabled = false }
	}
	else {
		apply plugin: 'org.springframework.boot'
		
		dependencies {
			compile project(":core")
		}
		
		clean.dependsOn( project(":core").clean )
		test.dependsOn( project(":core").test )
	}
	
	apply plugin: 'io.spring.dependency-management'
	
	group = 'org.snowjak'
	version = '0.0.1'
	sourceCompatibility = 10
	
	repositories {
		mavenCentral()
	}
}

task test(type: TestReport) {
	destinationDir = file("$buildDir/reports/allTests")
	
	dependsOn subprojects.collect( { it.tasks.withType(Test) } ).flatten()
	reportOn subprojects.collect( { it.tasks.withType(Test) } ).flatten()
}