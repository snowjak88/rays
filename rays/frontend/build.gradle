plugins {
	id 'com.ewerk.gradle.plugins.querydsl' version '1.0.9'
	id 'io.github.divinespear.jpa-schema-generate' version '0.3.5.1'
}

ext { vaadinVersion = '8.+' }

dependencies {
	compile('org.springframework.boot:spring-boot-starter-amqp')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-security')
	
	compile('com.vaadin:vaadin-spring-boot-starter')
	compile("com.vaadin:vaadin-push:${vaadinVersion}")
	compile('org.vaadin.artur:spring-data-provider:1.1.0')
	
	compile('com.querydsl:querydsl-jpa:4.1.4')
	compile('org.flywaydb:flyway-core:5.+')

	
	compile('javax.xml.bind:jaxb-api:2.3.0')
	
	
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
	
	runtime('org.hsqldb:hsqldb:2.4+')
	runtime('mysql:mysql-connector-java:8.+')
}

dependencyManagement { imports { mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}" } }

querydsl {
	querydslDefault = true
	jpa = true
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

generateSchema {
	vendor = 'hibernate+spring'
	packageToScan = [ 'org.snowjak.rays.frontend.model.entity' ]
	scriptAction = 'create'
	createOutputFileName = 'V1.0__Initial schema.sql'
	targets {
		generateMySQLSchema {
			outputDirectory = file("$projectDir/src/main/resources/db/migration/mysql")
			databaseProductName = 'org.hibernate.dialect.MySQL5Dialect'
			databaseMajorVersion = 5
			properties = [
				'hibernate.dialect': 'org.hibernate.dialect.MySQL5Dialect',
				'hibernate.dialect.storage_engine': 'innodb'
				]
		}
		generateHSQLDBSchema {
			outputDirectory = file("$projectDir/src/main/resources/db/migration/hsqldb")
			databaseProductName = 'HSQL Database Engine'
			properties = [
				'hibernate.dialect': 'org.hibernate.dialect.HSQLDialect'
				]
		}
	}
	
}